class Set {
    field int maxValue;
    field int maxIterations;
    field int width;
    field int height;

    /**
     * Due to the nature of Jack compiler
     * I was forced to "mangle" the arguments names, sorry
     */
    constructor Set new(int mxValue, int mxIterations, int wdth, int hght) {
        let maxValue = mxValue;
        let maxIterations = mxIterations;
        let width = wdth;
        let height = hght;

        return this;
    }

    /**
     * I'm using Escape Algorithm here.
     * The idea behind the algorithm is to compute the sequence of numbers
     * from the definition of Mandelbrot set iteratively
     * until a certain predefined maximum norm is reached
     * or we have reached some predefined maximum number of iterations.
     *
     * @see https://en.wikipedia.org/wiki/Mandelbrot_set#Escape_time_algorithm
     */
    method int getEscapeIterationsNumber(int pointX, int pointY) {
        var int currentIteration, x, y;
        var int xOfSquare, yOfSquare;

        let currentIteration = 0;
        let xOfSquare = 0;
        let yOfSquare = 0;
        let x = 0;
        let y = 0;

        while ((currentIteration < maxIterations) & (x * x + y * y < maxValue)) {
            // This particular optimization allows to skip multiplication
            // if one of operands are zero
            // because in that case, the result will be 0 anyway
            if (~((x = 0) | (y = 0))) {
                let xOfSquare = x * x - y * y;
                let yOfSquare = 2 * x * y;
            }

            let x = xOfSquare + pointX;
            let y = yOfSquare + pointY;
            let currentIteration = currentIteration + 1;
        }

        return currentIteration;
    }

    /**
     * Still, we need to compute the color of the iteration.
     * It is simple, we just need to spread the spectrum between "black" and "white".
     */
    method int getColorForIteration(int iterationNumber) {
        if (iterationNumber > (maxIterations / 2)) {
            return 1;
        } else {
            return 0;
        }
    }

    /**
     * The method we will be using for calculating the color for specific coordinate.
     */
    method int getColorForCoordinates(int x, int y) {
        return getColorForIteration(getEscapeIterationsNumber(x, y));
    }

    /**
     * The actual drawing of the set on the screen.
     * Nothing complex here, just iterating over coordinates
     * and calculating the color (black/white) for each of them.
     */
    method void draw() {
        var int x, y;

        do Screen.clearScreen();
        do Screen.setColor(true);

        let x = 0;
        let y = 0;
        while (y < height) {
            while (x < width) {
                if (getColorForCoordinates(x, y) = 1) {
                    do Screen.drawPixel(x, y);
                }

                let x = x + 1;
            }

            let x = 0;
            let y = y + 1;
        }

        return;
    }

    /**
     * Methods below are just for interaction.
     * They are used to change the coefficients in the set.
     */
    method void incMaxIterations() {
        let maxIterations = maxIterations + 1;
        return;
    }

    method void decMaxIterations() {
        let maxIterations = maxIterations - 1;
        return;
    }

    method void incMaxValue() {
        let maxValue = maxValue + 1;
        return;
    }

    method void decMaxValue() {
        let maxValue = maxValue - 1;
        return;
    }

    method void dispose() {
        do Memory.deAlloc(this);

        return;
    }
}
